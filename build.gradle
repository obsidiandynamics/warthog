plugins {
  id "com.github.johnrengelman.shadow" version "7.1.2"
  id "com.github.ben-manes.versions" version "0.42.0"
}
apply plugin: "maven-publish"

def versionFile(extension) {
  return new File("src/main/resources/" + project.name + extension)
}

def getVersion(extension) {
  def versionFile = versionFile extension
  if (! versionFile.exists()) return "0"
  def reader = new BufferedReader(new FileReader(versionFile))
  def version = reader.readLine().trim()
  reader.close()
  return version
}

def setVersion(extension, version) {
  def versionFile = versionFile extension
  def writer = new BufferedWriter(new FileWriter(versionFile))
  writer.write(String.valueOf(version))
  writer.close()
}

def packageVersion = getVersion ".version"
def packageBuild = getVersion ".build"

task getBuild() {
  doLast {
    println "At ${getVersion('.version')}_${getVersion('.build')}"
  }
}

task setBuild() {
  doLast {
    def timestamp = new java.text.SimpleDateFormat("yyMMddHHmm").format(new Date())
    setVersion(".build", timestamp)
  }
}

group = "com.obsidiandynamics.warthog"
version = packageVersion

def baseJvmArgs = [
  "--add-opens=java.base/java.io=ALL-UNNAMED",
  "--add-opens=java.base/java.lang=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.invoke=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
  "--add-opens=java.base/java.nio=ALL-UNNAMED",
  "--add-opens=java.base/java.nio.charset=ALL-UNNAMED",
  "--add-opens=java.base/java.util.concurrent.atomic=ALL-UNNAMED",
  "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
  "-XX:-MaxFDLimit",
  "-XX:+TieredCompilation",
  "-XX:+UseNUMA",
  "-XX:+UseCondCardMark",
  "-Djava.net.preferIPv4Stack=true"
]
project.ext.set("baseJvmArgs", baseJvmArgs)

allprojects {
  apply plugin: "java-library"
  apply plugin: "jacoco"
  apply plugin: "maven-publish"
  apply plugin: "com.github.johnrengelman.shadow"
  
  sourceCompatibility = 10 
  targetCompatibility = 10

  repositories {
    mavenCentral()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
    }
  }
  
  ext {
    commonslangVersion = "3.8"
    equalsverifierVersion = "3.10"
    fulcrumVersion = "0.37.0"
    jacksonVersion = "2.9.6"
    jansiVersion = "1.17.1"
    jcommanderVersion = "1.72"
    junitVersion = "4.12"
    httpasyncclientVersion = "4.1.4"
    httpclientVersion = "4.5.6"
    mockitoVersion = "4.6.1"
    pojotesterVersion = "0.9.0"
    yconfVersion = "0.21.0"
    
    repoDir = "${project(':').projectDir}/../../repo"
  }
  
  dependencies {
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-assert:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-await:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-junit:${fulcrumVersion}"
    testImplementation "com.obsidiandynamics.fulcrum:fulcrum-verifier:${fulcrumVersion}"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "nl.jqno.equalsverifier:equalsverifier:${equalsverifierVersion}"
    testImplementation "com.obsidiandynamics.pojotester:core:${pojotesterVersion}"
  }
  
  test {
    include "**/*Test.class"
    jvmArgs += baseJvmArgs
  }
  
  task integrationTest(type: Test, description: "Runs integration tests", group: "Verification") {
    include "**/*IT.class"
    jvmArgs += baseJvmArgs
  }
  
  jacoco {
    toolVersion = "0.8.8"
  }

  task verify {
    dependsOn test
    dependsOn integrationTest
  }

  jacocoTestReport {
    additionalSourceDirs.from = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(sourceSets.main.output)
    executionData.from = files("$buildDir/jacoco/test.exec") + file("$buildDir/jacoco/integrationTest.exec")
    reports {
      html.required = true
      xml.required = true
      csv.required = false
    }
  }

  javadoc {
    if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
      options.addStringOption("quiet", "-html5")
    }
  }
}

subprojects {
}

dependencies {
  api "com.obsidiandynamics.fulcrum:fulcrum-func:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-io:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-props:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-shell:${fulcrumVersion}"
  api "com.obsidiandynamics.fulcrum:fulcrum-version:${fulcrumVersion}"
  api "com.obsidiandynamics.yconf:yconf-core:${yconfVersion}"
  api "com.obsidiandynamics.yconf:yconf-snakeyaml:${yconfVersion}"
  api "org.apache.httpcomponents:httpclient:${httpclientVersion}"
  api "org.apache.httpcomponents:httpasyncclient:${httpasyncclientVersion}"
  api "org.apache.commons:commons-lang3:${commonslangVersion}"
  api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  api "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
  api "com.beust:jcommander:${jcommanderVersion}"
  api "org.fusesource.jansi:jansi:${jansiVersion}"
}

task jacocoRootReport(type: JacocoReport) {
  mustRunAfter = allprojects.test + allprojects.integrationTest + allprojects.javadoc
  additionalSourceDirs.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(allprojects.sourceSets.main.output)
  executionData.setFrom project.fileTree(dir: '.', include: ['**/build/jacoco/test.exec', '**/build/jacoco/integrationTest.exec'])
  reports {
    html.required = true
    xml.required = true
    csv.required = false
  }
  onlyIf = {
    true
  }
  afterEvaluate {
    classDirectories.from = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ["sample/**", "**/*Uncovered*"])
    })
  }
}
 
artifacts {
  archives jar
  archives shadowJar
}

def packageName = "warthog"

jar {
  baseName packageName
  finalizedBy jacocoRootReport
}
